% Generating Irrigation and Pesticide inputs in Nigeria General Household Survey, Panel 2012-2013, Wave 2
% Emil Kee-Tui 
% `s c(current_date)`

Farm output is likely to be positively affected by irrigation and whether pesticide was applied. This chapter guides the creation of the binary variable for irrigation and pesticide use. The dataset contains information on the type of irrigation, and how many season during the year the plot is irrigated, and if the plot is irrigated. We do not have a fine measure of the water output of the irrigation system. Not all wells are the same, two wells may be capable of producing different volumes of water, volume of water produced an hour is an accurate measure of the usefulness of an irrigation system, we do not have volume per hour, so including a variable for the type of irrigation system is not of much help at calculating output of irrigatino. We can be sure that some type of irrigation will at least have a non-deterimental effect on production, some irrigation will likely improve production, so we should include a binary for irrigation in our estimation. 

The file we want to use is **sect11b1_plantingw2.dta**. The file contains a lot of variables that do not relate to irrigation, we will drop those extra variables at the end and have a streamlined dataset on irrigation.

```{s}
use "$root/wave_2/raw/sect11b1_plantingw2.dta", clear
```

The irrigation information relates to irrigation on a single plot belonging to a household. We will eventually want to merge the irrigation data with labor, output, other inputs, etc. It makes sense that we want to match plot inputs to plot outputs. Observations are identified geographically by zone, state, LGA, sector, EA, and then identified by a household ID number and a plot ID. Houshold id's are unique across the dataset, while plot id's are unique to the households, so we can uniquely identify plots by the household id and then by the plot id. 

We can confirm that we can uniquely identify by the household id and then by the plot id by the `isid` command. `isid` will produce no output if we can uniquely identify the data by **hhid** and **plotid**, or else an error will be displayed if we cannot uniquely identify the data in that way.

```{s}
	isid			hhid plotid
```

The variable **s11b1q39** is a simple binary showing if that plot is irrigated. Let's rename in order to determine the purpose of the variable without having to look at the label, we'll also change the label to pricisely tell us how the binary is encoded. The binary displays 'Yes' or 'No', the actual data is encoded with '1' and '2' for yes-no.

```{s}
	rename			s11b1q39 irr_any
	lab var			irr_any "=1 if any irrigation was used"
```

Some observations are missing for a number of reasons. Without other irrigation data on the missing observations we have to assume that the missing variables are missing because there is no irrigation on that plot. So let's change the missing observations to '2' for 'no'.

```{s}
	count if			irr_any==.
	replace			irr_any = 2 if irr_any == .
```

We are done with the input variables relating to irrigation. Clean up the dataset dropping all variables except the **irr_any** variable and id variables.
	
```{s}
	keep 			hhid zone state lga sector hhid ea plotid irr_any tracked_obs
```

Just before saving we want to make a **plot_id** variable that uniquely identifies the observations by the hhid and the plotid. We use the `egen` function to create a new variable that groups the **hhid** and **plotid** into one number.

```{s}
	egen			plot_id = group(hhid plotid)
	lab var			plot_id "unique plot identifier"
```

Now we have everything lets save the file into a new file location for all the cleaned datasets.

```{s}
	save "$root/wave_2/refined/pp_sect11b1.dta", replace	
```

# Pesticide and Herbicide

We will produce two binary variables for herbicide and pesticide that shows "yes" if herbicide or pesticide was used on the plot. The file contains a variable for the amount of pesticide and herbicide used, the amount of pesticide and pesticide is significant to the effect of the pesticide and herbicide. However there are very few plots which received pesticide or herbicide, herbicide was applied to 11 plots and pesticide was applied to 6 plots. There are too few instances of herbicide or pesticide application in order to be statistically signicant or draw a statistical conclusion about their effect on production. Pesticide and herbicide variables will be less useful in this dataset, so we will not take the additional steps to prepare more variables relating to pesticide and herbicide for this particular dataset.

The file we want to use is **sect11c2_plantingw2.dta**. The file contains a number of variables we will mostly discard.
```{s}
	use "$root/wave_2/raw/sect11c2_plantingw2.dta", clear
```

The data is at the household-plot level, this means each household has a unique ID and each plot used or belonging to a household is uniquely identified within the household. We should be able to identify the observations by the household ID and then the plot ID. We can confirm the data structure with the `isid` command, which will not produce a display if the data is uniquely identified by household ID and plot ID.

```{s}
	isid			hhid plotid
```

The variable **s11c2q1** is a binary asking if pesticide was used on the plot. We should rename the variable so we know what it relates to without having to look at the label, we will also want to relabel it.

```{s}
	rename			s11c2q1 pest_any
	lab var			pest_any "=1 if any pesticide was used"
```

The variable **s11c2q10** is a binary asking if herbicide was used on the plot. Rename it as well and relabel it too.

```{s}
	rename			s11c2q10 herb_any
	lab var			herb_any "=1 if any herbicide was used"
```

There may be several missing observations relating to our newly renamed variables. We can determine if the observation is correctly missing by checking the missing variables against the variables for quantity of pest and herb. If the observation is correctly missing then the quantity variable will be 0 or missing.

The variable **s11c2q2a** relates to the quantity of pesticide and **s11c2q11a** relates to the quantity of herbicide. Rename these variables so they are easier to read.

```{s}
	rename 			s11c2q2a 	pest_quant
	rename 			s11c2q11a 	herb_quant
```

We want to look at the observations for quantity for missing variables. That is done by `tab` which displays data in the observation, for the observations that meet our criteria, which is the criteria that the associated pest_any or herb_any observation is missing.

```{s}
	tab 			pest_quant  if pest_any == .
	tab 			herb_quant 	if herb_any == .
```

There are no observations, meaning that the observations are correctly missing, so lets change the missing observations to the binary response for **pest_any** and **herb_any** to "no".

```{s}
	replace			pest_any = 2 if pest_any == .
	replace			herb_any = 2 if herb_any == .
```

We are finished making the pesticide and herbicide binary. So, drop all the variables we do not need and save the data in the file for cleaned data.
	
```{s}
	keep 			hhid zone state lga sector hhid ea plotid pest_any herb_any tracked_obs
```

Just before saving we want to make a **plot_id** variable that uniquely identifies the observations by the hhid and the plotid. We use the `egen` function to create a new variable that groups the **hhid** and **plotid** into one number.

```{s}
	egen			plot_id = group(hhid plotid)
	lab var			plot_id "unique plot identifier"
```

```{s}
	save "$root/wave_2/refined/pp_sect11c2.dta", replace	
```

